PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX media-image: <https://www.iana.org/assignments/media-types/image/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# COLUMNS
# painting_id
# title
# description
# har_nbr
# image_name (filename)
# thumb_name (filename)
# create_user_id
# create_date
# last_modified_user_id
# last_modified_date
# publish_type_id (ignore)
# is_locked (ignore)
# lock_user_id (ignore)
# lock_date (ignore)
# source
# source_url
# type
# century
# tradition (ignore)
# tradition_id 
# tbrc_gid
# artist_name
# artist_tbrc_pid

# Run: 
# tarql -e utf-8 images.tq painting-8-5-24.csv > tolImages.ttl

CONSTRUCT {

    ?image_iri
        a gist:FormattedContent ;
        skos:prefLabel ?title_en ;
        gist:description ?description_en ;
        gist:isConnectedTo ?tradition_iri ,
            ?historical_period_iri ;
        # TODO This really pertains to the subject of the image, not the image itself (e.g., the file is an image of a sculpture, the file itself is not a sculpture)
        gist:isCategorizedBy ?work_type_iri ;
        tolo:sourceUrl ?source_url_anyURI ;
        tolo:credit ?credit_en ;
        gist:isIdentifiedBy ?har_number_iri ;

        # File metadata
        tolo:fileName ?image_name ;
        tolo:hasThumbnail ?thumbnail_iri ;
        gist:isExpressedIn ?file_media_type_iri ;

        # Editing metadata
        tolo:isCreatedBy ?created_by_iri ;
        tolo:createDateTime ?create_datetime ;
        tolo:isLastModifiedBy ?last_modified_by_iri ;
        tolo:lastModifiedDateTime ?last_modified_datetime ;
        .

    ?thumbnail_iri 
        a tolo:FormattedContent ;
        skos:prefLabel ?title_en ;
        tolo:fileName ?thumb_file_name ;
        gist:isExpressedIn ?thumb_media_type_iri ;
        .

    ?har_number_iri
        a gist:ID ;
        skos:prefLabel ?har_label ;
        gist:isCategorizedBy tolx:_IdType_har ;
        gist:idText ?har_number ;
        .

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1)

    # Use image filename for IRIs to ensure uniqueness.
    BIND(LCASE(REPLACE(REPLACE(REPLACE(?image_name, "['()]", ""), "\\.[^.]+$", ""), "\\W", "_")) AS ?filename)

    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_", ?filename)) AS ?image_iri)

    BIND(LCASE(REPLACE(?image_name, "^.+\\.", "")) AS ?file_ext)
    BIND(
        COALESCE(
            IF(?file_ext IN ("jpg", "jpeg"), media-image:jpg, ?unbound),
            IF(?file_ext = "png", media-image:png, ?unbound)
        ) AS ?file_media_type_iri
    )

    # Thumbnail
    BIND(LCASE(REPLACE(REPLACE(REPLACE(?thumb_name, "['()]", ""), "\\.[^.]+$", ""), "\\W", "_")) AS ?thumb_filename)
    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent_Thumbnail_", ?thumb_filename)) AS ?thumbnail_iri)
    BIND(LCASE(REPLACE(?thumb_name, "^.+\\.", "")) AS ?thumb_file_ext)
    BIND(
        COALESCE(
            IF(?thumb_file_ext IN ("jpg", "jpeg"), media-image:jpg, ?unbound),
            IF(?thumb_file_ext = "png", media-image:png, ?unbound)
        ) AS ?thumb_media_type_iri
    )

    # Name
    BIND(STRLANG(?title, "en") AS ?title_en)

    # Description
    BIND(STRLANG(?description, "en") AS ?description_en)

    # HAR URL
    BIND(tarql:expandPrefixedName(CONCAT("told:_ID_har_", ?har_nbr)) AS ?har_number_iri)
    BIND(CONCAT("HAR Item Number ", ?har_nbr) AS ?har_label)

    BIND(tarql:expandPrefixedName(CONCAT("tolx:_WorkType_", ?type)) AS ?work_type_iri)

    BIND(tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?century)) AS ?century_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", ?tradition_id)) AS ?tradition_iri)

    # Sources and credits
    BIND(STRDT(?source_url, xsd:anyURI) AS ?source_url_anyURI)
    BIND(STRLANG(?source, "en") AS ?credit_en)

    # Editing metadata
    BIND(tarql:expandPrefixedName(CONCAT("told:_User_", ?create_user_id)) AS ?created_by_iri)
    BIND(STRDT(REPLACE(?create_date, " ", "T"), xsd:dateTime) AS ?create_datetime)

    BIND(tarql:expandPrefixedName(CONCAT("told:_User_", ?last_modified_user_id)) AS ?last_modified_by_iri)
    BIND(STRDT(REPLACE(?last_modified_date, " ", "T"), xsd:dateTime) AS ?last_modified_datetime)



}
