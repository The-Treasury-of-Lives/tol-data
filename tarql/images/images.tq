PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX media-image: <https://www.iana.org/assignments/media-types/image/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# COLUMNS
# painting_id
# title
# description
# har_nbr
# image_name (filename)
# thumb_name (filename)
# create_user_id
# create_date
# last_modified_user_id
# last_modified_date
# publish_type_id (ignore)
# is_locked (ignore)
# lock_user_id (ignore)
# lock_date (ignore)
# location
# source
# source_original (temporary - ignore)
# source_url
# creator_name
# creator_tol_id
# type
# century
# tradition_id 
# tol_place_id

# Run: 
# tarql -e utf-8 images.tq painting-9-6-24.csv > tolImages.ttl

CONSTRUCT {

    ?image_file_iri
        a tolo:Image ;
        skos:prefLabel ?title_en ;
        tolo:depicts ?work_iri ;
        tolo:fileName ?image_name ;
        tolo:hasThumbnail ?thumbnail_iri ;
        gist:isExpressedIn ?media_type_iri ;
        gist:hasAddress ?source_iri ;
        tolo:credit ?credit_en ;

        # Editing metadata
        tolo:isCreatedBy ?created_by_iri ;
        tolo:createDateTime ?create_datetime ;
        tolo:isLastModifiedBy ?last_modified_by_iri ;
        tolo:lastModifiedDateTime ?last_modified_datetime ;
        .

    ?work_iri   
        a gist:IntellectualProperty ;
        skos:prefLabel ?title_en ;
        gist:description ?description_en ;
        gist:isConnectedTo ?tradition_iri ,
            ?historical_period_iri ,
            ?place_iri ;
        gist:isCategorizedBy ?work_type_iri ;
        gist:isIdentifiedBy ?har_number_iri ;
        gist:hasPhysicalLocation ?work_location_iri ;
        tolo:isCreatedBy ?creator_iri ;
        .

    ?creator_iri    
        a ?person_class ;
        skos:prefLabel ?creator_name_en ;
        gist:name ?creator_name_en ;
        . 

    ?work_location_iri   
        a gist:Place ;
        skos:prefLabel ?location_label_en ;
        gist:name ?location_label_en ;
        .

    ?thumbnail_iri 
        a tolo:Image ;
        skos:prefLabel ?title_en ;
        tolo:fileName ?thumb_name ;
        gist:isExpressedIn ?media_type_iri ;
        .

    ?har_number_iri
        a gist:ID ;
        skos:prefLabel ?har_label ;
        gist:isCategorizedBy tolx:_IdType_har ;
        gist:uniqueText ?har_nbr ;
        .

    ?source_iri
        a gist:ElectronicAddress ;
        skos:prefLabel ?source_en ;
        gist:isCategorizedBy 
            tolx:_ElectronicAddressType_url ,
            tolx:_AddressUsageType_source ;
        # Can't use gist:containedText since its range is xsd:string.
        tolo:url ?source_address ;
        .


}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 1)

    BIND(tarql:expandPrefixedName(CONCAT("told:_Image_", ?painting_id)) AS ?image_file_iri)
    BIND(tarql:expandPrefixedName(CONCAT("told:_IntellectualProperty_", ?painting_id)) AS ?work_iri)

    BIND(LCASE(REPLACE(?image_name, "^.+\\.", "")) AS ?file_ext)
    BIND(
        COALESCE(
            IF(?file_ext IN ("jpg", "jpeg"), media-image:jpg, ?unbound),
            IF(?file_ext = "png", media-image:png, ?unbound)
        ) AS ?media_type_iri
    )

    # Thumbnail
    BIND(tarql:expandPrefixedName(CONCAT("told:_Image_Thumbnail_", ?painting_id)) AS ?thumbnail_iri)

    # Name
    BIND(STRLANG(?title, "en") AS ?title_en)

    # Description
    BIND(STRLANG(?description, "en") AS ?description_en)

    # HAR URL
    BIND(tarql:expandPrefixedName(CONCAT("told:_ID_har_", ?har_nbr)) AS ?har_number_iri)
    BIND(CONCAT("HAR ", ?har_nbr) AS ?har_label)

    # Work category
    BIND(tarql:expandPrefixedName(CONCAT("tolx:_WorkType_", ?type)) AS ?work_type_iri)

    # Creator
    BIND(IF(BOUND(?creator_tol_id), tarql:expandPrefixedName(CONCAT("told:_Person_", ?creator_tol_id)), ?unbound) AS ?existing_creator_iri)
    BIND(IF(!BOUND(?existing_creator_iri) && BOUND(?creator_name), tarql:expandPrefixedName(CONCAT("told:_Person_", LCASE(REPLACE(REPLACE(?creator_name, "[,\\.]", ""), " ", "_")))), ?unbound) AS ?new_creator_iri)
    BIND(IF(BOUND(?new_creator_iri), STRLANG(?creator_name, "en"), ?unbound) AS ?creator_name_en)
    BIND(IF(BOUND(?new_creator_iri), tarql:expandPrefixedName("gist:Person"), ?unbound) AS ?person_class)
    BIND(COALESCE(?existing_creator_iri, ?new_creator_iri) AS ?creator_iri)

    # Historical period
    BIND(IF(?century != "0", tarql:expandPrefixedName(CONCAT("told:_HistoricalPeriod_", ?century)), ?unbound) AS ?historical_period_iri)

    # Traditions
    OPTIONAL { ?tradition_ID apf:strSplit(?tradition_id "; ") }
    BIND(tarql:expandPrefixedName(CONCAT("told:_Tradition_", ?tradition_ID)) AS ?tradition_iri)

    # Related place
    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", ?tol_place_id)) AS ?place_iri)

    # Physical location of work (museum, collection, etc.)
    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", LCASE(REPLACE(?location, " ", "_")))) AS ?work_location_iri)
    BIND(STRLANG(?location, "en") AS ?location_label_en)

    # If there's a source URL, create an address object.
    BIND(IF(BOUND(?source_url), tarql:expandPrefixedName(CONCAT("told:_ElectronicAddress_source_image_", ?painting_id)), ?unbound) AS ?source_iri)
    BIND(IF(BOUND(?source_url), STRDT(?source_url, xsd:anyURI), ?unbound) AS ?source_address)
    BIND(STRLANG(?source, "en") AS ?source_en)

    # If there's source text with no URL, and it's different from the location, make it the credit.
    BIND(IF(!BOUND(?source_url) && (!BOUND(?location) || ?source != ?location), ?source_en, ?unbound) AS ?credit_en)

    # Editing metadata
    BIND(tarql:expandPrefixedName(CONCAT("told:_User_", ?create_user_id)) AS ?created_by_iri)
    BIND(REPLACE(REPLACE(?create_date, " ", "T"), "T(\\d):", "T0$1:") AS ?create_date_clean)
    BIND(STRDT(?create_date_clean, xsd:dateTime) AS ?create_datetime)

    BIND(tarql:expandPrefixedName(CONCAT("told:_User_", ?last_modified_user_id)) AS ?last_modified_by_iri)
    BIND(REPLACE(REPLACE(?last_modified_date, " ", "T"), "T(\\d):", "T0$1:") AS ?last_modified_date_clean)
    BIND(STRDT(?last_modified_date_clean, xsd:dateTime) AS ?last_modified_datetime)

}
