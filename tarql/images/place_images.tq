PREFIX bdo: <http://purl.bdrc.io/ontology/core/>
PREFIX bdr: <http://purl.bdrc.io/resource/>
PREFIX bf: <http://id.loc.gov/ontologies/bibframe/>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX gist: <https://w3id.org/semanticarts/ns/ontology/gist/>
PREFIX media-txt: <https://www.iana.org/assignments/media-types/text/>
PREFIX media-image: <https://www.iana.org/assignments/media-types/image/> 
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX told: <https://w3id.org/treasuryoflives/ns/data/tol/>
PREFIX tolo: <https://w3id.org/treasuryoflives/ns/ontology/tol/>
PREFIX tolx: <https://w3id.org/treasuryoflives/ns/taxonomy/tol/>
PREFIX wiki: <https://www.wikidata.org/wiki/>
PREFIX xml: <http://www.w3.org/XML/1998/namespace>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

# COLUMNS
# community_id
# community_name
# image_credit
# image_date_taken
# image_license
# image_source_url
# image_path

# Run: 
# tarql -e utf-8 place_images.tq place-image-3-18-24.csv > tolPlaceImages.ttl

CONSTRUCT {

    ?image_iri
        a gist:FormattedContent ;
        skos:prefLabel ?image_label_en ;
        gist:isAbout ?place_iri ;
        tolo:credit ?credit_en ;
        gist:license ?license_en ;      
        tolo:sourceUrl ?source_url ;
        tolo:creationDate ?creation_date ;
        tolo:filePath ?image_path ;
        gist:isCategorizedBy ?media_type_iri ;
        .

}

WHERE {
    
    # Testing
    # FILTER(?ROWNUM = 5)

    # Use image path for IRIs to ensure uniqueness
    BIND(LCASE(REPLACE(REPLACE(REPLACE(?image_path, "['()]", ""), "\\W", "_"), "\\.[^.]+$", "")) AS ?filename)
    BIND(tarql:expandPrefixedName(CONCAT("told:_FormattedContent", ?filename)) AS ?image_iri)
    BIND(LCASE(REPLACE(?image_path, "^.+\\.", "")) AS ?image_file_ext)
    BIND(
        COALESCE(
            IF(?image_file_ext IN ("jpg", "jpeg"), media-image:jpg, ?unbound),
            IF(?image_file_ext = "png", media-image:png, ?unbound)
        ) AS ?media_type_iri
    )

    BIND(STRLANG(?community_name, "en") AS ?image_label_en)
    
    BIND(tarql:expandPrefixedName(CONCAT("told:_Place_", ?community_id)) AS ?place_iri)

    BIND(IF(?image_credit != "NULL", STRLANG(?image_credit, "en"), ?unbound) AS ?credit_en)

    BIND(IF(?image_license != "NULL", ?image_license, ?unbound) AS ?license_en)

    BIND(IF(?image_source_url != "NULL", STRDT(?image_source_url, xsd:anyURI), ?unbound) AS ?source_url)

    BIND(IF(?image_date_taken != "NULL", STRDT(CONCAT(?image_date_taken, "T00:00:00"), xsd:dateTime), ?unbound) as ?creation_date)

}
